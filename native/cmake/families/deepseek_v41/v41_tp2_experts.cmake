# TP2 RTX backbone experts coexist with dSpark and full-width RTX variants.
if(NOT CUTEAFD_ENABLE_V41_EXPERT_AOT OR NOT CUTEAFD_V41_EXPERT_ROLE STREQUAL "coordinator")
  message(FATAL_ERROR "TP2 V4.1 experts require the SM120 coordinator expert build")
endif()
set(CUTEAFD_V41_TP2_EXPERT_DIR "${CMAKE_CURRENT_BINARY_DIR}/v41_tp2_experts")
set(CUTEAFD_V41_TP2_EXPERT_OBJECTS)
set(CUTEAFD_V41_TP2_EXPERT_HEADERS)
set(CUTEAFD_V41_TP2_COMPACT_ARGS)
if(CUTEAFD_V41_TP2_COMPACT_EXPERIMENT)
  list(APPEND CUTEAFD_V41_TP2_COMPACT_ARGS --compact-max-capacity 16 --compact-live-rows 8)
endif()
foreach(rows IN ITEMS 1 16 80 256 1024 4096)
  set(stem "${CUTEAFD_V41_TP2_EXPERT_DIR}/v41_rtx_tp2_m${rows}")
  list(APPEND CUTEAFD_V41_TP2_EXPERT_OBJECTS "${stem}.o")
  list(APPEND CUTEAFD_V41_TP2_EXPERT_HEADERS "${stem}.h")
endforeach()
add_custom_command(
  OUTPUT "${CUTEAFD_V41_TP2_EXPERT_DIR}/v41_experts.json"
    "${CUTEAFD_V41_TP2_EXPERT_DIR}/v41_tp2_expert_variants.h"
    ${CUTEAFD_V41_TP2_EXPERT_OBJECTS} ${CUTEAFD_V41_TP2_EXPERT_HEADERS}
  COMMAND ${CUTEAFD_SPARKINFER_VERIFY_COMMAND}
  COMMAND "${CMAKE_COMMAND}" -E env ${CUTEAFD_SPARKINFER_PYTHON_ENV}
    "${Python3_EXECUTABLE}"
    "${CMAKE_CURRENT_SOURCE_DIR}/../python/tools/export_b12x_v41_slices_aot.py"
    --output-dir "${CUTEAFD_V41_TP2_EXPERT_DIR}" --role rtx_tp2
    --rows 1,16,80,256,1024,4096 --width 192 --atomic-min-capacity 256 --standard-names
    ${CUTEAFD_V41_TP2_COMPACT_ARGS}
  COMMAND "${CMAKE_COMMAND}" -E copy
    "${CUTEAFD_V41_TP2_EXPERT_DIR}/v41_expert_variants.h"
    "${CUTEAFD_V41_TP2_EXPERT_DIR}/v41_tp2_expert_variants.h"
  DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/../python/tools/export_b12x_v41_slices_aot.py"
    "${CMAKE_CURRENT_SOURCE_DIR}/../python/tools/v41_spark_tp3_launch_geometry.py"
    "${CMAKE_CURRENT_SOURCE_DIR}/../python/tools/export_b12x_v41_experts_aot.py"
    ${CUTEAFD_SPARKINFER_PROVENANCE_INPUTS} ${CUTEAFD_SPARKINFER_EXPORT_INPUTS}
  COMMENT "Exporting TP2 RTX backbone expert kernels"
  VERBATIM
)
add_custom_target(cuteafd_v41_tp2_experts_export DEPENDS
  "${CUTEAFD_V41_TP2_EXPERT_DIR}/v41_tp2_expert_variants.h"
  "${CUTEAFD_V41_TP2_EXPERT_DIR}/v41_experts.json"
  ${CUTEAFD_V41_TP2_EXPERT_OBJECTS} ${CUTEAFD_V41_TP2_EXPERT_HEADERS})
add_dependencies(cuteafd_v41_tp2_experts_export cuteafd_verify_sparkinfer_source)
set_source_files_properties(${CUTEAFD_V41_TP2_EXPERT_OBJECTS} PROPERTIES
  EXTERNAL_OBJECT TRUE GENERATED TRUE)
list(APPEND CUTEAFD_NATIVE_SOURCES ${CUTEAFD_V41_TP2_EXPERT_OBJECTS} families/deepseek_v41/src/v41_tp2_experts.cc)
